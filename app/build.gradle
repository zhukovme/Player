apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: "de.mannodermaus.android-junit5"
apply from: "$rootDir/tools/verification/lint-config.gradle"
apply from: "$rootDir/tools/versioning/versioning.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    def versionProps = getVersionProps()

    defaultConfig {
        applicationId "com.zhukovme.player"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode buildVersionCode(versionProps)
        versionName buildVersionName(versionProps)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }

            jacocoOptions {
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
                unitTests.all {
                    testLogging.events = ["passed", "skipped", "failed"]
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation deps('org.jetbrains.kotlin:kotlin-stdlib-jdk8')

    // Android
    implementation deps('androidx.appcompat:appcompat')
    implementation deps('com.google.android.material:material')
    implementation deps('androidx.constraintlayout:constraintlayout')

    // Firebase
    implementation deps('com.google.firebase:firebase-core')
    implementation deps('com.crashlytics.sdk.android:crashlytics')

    // Rx
    implementation deps('io.reactivex.rxjava2:rxjava')
    implementation deps('io.reactivex.rxjava2:rxandroid')
    implementation project(path: ':rxelm')

    // Inversion of control
    implementation deps('org.kodein.di:kodein-di-generic-jvm')
    implementation deps('org.kodein.di:kodein-di-framework-android-x')

    // Logger
    implementation deps('com.jakewharton.timber:timber')

    // Memory leak detection
    debugImplementation deps('com.squareup.leakcanary:leakcanary-android')

    // Unit tests
    testImplementation deps('org.jetbrains.kotlin:kotlin-reflect')
    testImplementation deps('org.spekframework.spek2:spek-dsl-jvm')
    testImplementation deps('org.spekframework.spek2:spek-runner-junit5')
    testImplementation deps('org.assertj:assertj-core')

    // Android tests
    androidTestImplementation deps('androidx.test:runner')
    androidTestImplementation deps('androidx.test.espresso:espresso-core')
}

apply plugin: 'com.google.gms.google-services'
